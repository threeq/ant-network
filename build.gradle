buildscript {
	ext {
        springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenLocal()
		mavenCentral()
        jcenter()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
        maven { url "http://repo.spring.io/libs-snapshot-local" }
	}
	dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath('se.transmode.gradle:gradle-docker:1.2')
        classpath("io.spring.gradle:dependency-management-plugin:1.0.4.RELEASE")
		classpath('org.junit.platform:junit-platform-gradle-plugin:1.0.0-RC3')
	}
}

group = 'threeq'
version = '0.0.1-SNAPSHOT'
description = "呵呵呵"

allprojects {
	apply plugin: 'java'
	apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
	apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8

    repositories {
		mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }
        maven { url 'https://repo.spring.io/release/' }
        maven { url 'https://repo.spring.io/libs-milestone' }
        maven { url "http://repo.spring.io/libs-snapshot-local" }
        maven { url "http://repo.spring.io/libs-milestone-local" }
        maven { url "http://repo.spring.io/libs-release-local" }
    }

    ext {
        springCloudVersion = 'Finchley.M8'
		junitJupiterVersion="5.1.0"
		junitPlatformVersion="1.1.0"
		junitVintageVersion="5.1.0"
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

	configurations {
//	all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
//	all*.exclude group: 'ch.qos.logback'
	}

	dependencies {
        compileOnly('org.projectlombok:lombok')
	}
}

/**
 * 定义项目依赖关系
 * common <--- test <------------
 *    ^		    ^               |
 *    |         |               |
 *	  ---------core <----- <其他业务子工程>
 *
 */
project(':threeq-libs-test') {
	dependencies {
		compile(
				project(":threeq-libs-common")
		)
	}
}
project(':threeq-libs-core') {
	dependencies {
		compile(
				project(":threeq-libs-common")
		)
		testCompile(
				project(":threeq-libs-test")
		)
	}
}
configure(subprojects.findAll {
	![
			'threeq-libs-core',
			'threeq-libs-common',
			'threeq-libs-test'
	].contains(it.name)
}) {
	apply plugin: 'docker'

	task buildDocker(type: Docker, dependsOn: build) {
		push = false
		applicationName = jar.baseName
		dockerfile = file('src/main/docker/Dockerfile')
		doFirst {
			copy {
				from jar
				into stageDir
			}
		}
	}

	dependencies {
		compile(
				project(":threeq-libs-common"),
				project(":threeq-libs-core")
		)
		testCompile(
				project(":threeq-libs-test")
		)
	}
}

